import groovy.xml.Namespace
import groovy.xml.XmlUtil

apply plugin: 'com.android.application'
apply plugin: 'maven'

def vn = '1.06.00'
def vc = vn.replace('.', '').toInteger()

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    signingConfigs {
        release {
            keyAlias 'iptv'
            keyPassword '709394'
            storeFile file('./release.keystore')
            storePassword '709394'
        }
    }

    defaultConfig {
        applicationId "linkin.com.apkinfo"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode vc
        versionName vn
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
        }
    }

    productFlavors {

    }

    lintOptions {
        abortOnError false
    }
}

/************************ 搜集项目信息 - 开始 ************************/

/**************** 方法定义 - 开始 ****************/

// 包名
def pkgName() {
    return String.valueOf(android.defaultConfig.applicationId)
}

// 版本名
def versionName() {
    return String.valueOf(android.defaultConfig.versionName)
}

// 版本号
def versionCode() {
    return String.valueOf(android.defaultConfig.versionCode)
}

// 最低支持的 Sdk 版本
def minSdk() {
    return String.valueOf(android.defaultConfig.minSdkVersion.mApiLevel)
}

// 当前所处的 Git 分支名称
def gitBranch() {
    return "git rev-parse --abbrev-ref HEAD".execute().text.trim()
}

// 当前打包时对应的 Commit Id
def gitCommitId() {
    return "git rev-parse HEAD".execute().text.trim()
}

// 是否为系统应用（是否属于系统用户组）
def isSystemApp() {
    return "android.uid.system".equals(userId())
}

// 获取用户组
def userId() {
    def manifestFile = file(project.projectDir.absolutePath + '/src/main/AndroidManifest.xml')
    // 需要 import groovy.xml.Namespace
    def ns = new Namespace("http://schemas.android.com/apk/res/android", "android")
    def xml = new XmlParser().parse(manifestFile)
    return xml.attributes()[ns.sharedUserId].toString()
}

// 判断是否还有变更没有提交
def needCommit() {
    def output = "git status --porcelain".execute().text.trim()
    if (output.isEmpty()) return false
    return !(output.size() == 1 && output.contains('appinfo.xml'))
}

// 判断版本名是否以00结尾（通常来说，release版本号需要以00结尾）
def endsWith00() {
    return versionName().endsWith('00')
}

/**************** 方法定义 - 结束 ****************/

/**************** 任务定义 - 开始 ****************/

// 搜集项目信息的任务
task createAppInfo << {

    // 创建记录项目基本信息的文件，以供后台系统读取
    def assets = project.projectDir.absolutePath + '/src/main/assets/'
    def apkInfo = assets + "appinfo.xml"

    def directory = new File(assets)
    if (!directory.exists()) {
        directory.mkdirs()
    }

    def file = new File(apkInfo)
    file.createNewFile()

    // 获取最基本的项目信息
    def packageName = pkgName()
    def versionName = versionName()
    def versionCode = versionCode()
    def minSdk = minSdk()
    def gitBranch = gitBranch()
    def gitCommitId = gitCommitId()
    def isSystemApp = isSystemApp()

    // 记录上述的基本信息
    def info = "<project name='${rootProject.name}'>\n" +
            "\t<package_name>${packageName}</package_name>\n" +
            "\t<version_name>${versionName}</version_name>\n" +
            "\t<version_code>${versionCode}</version_code>\n" +
            "\t<channel_id>release</channel_id>\n" +
            "\t<min_sdk>${minSdk}</min_sdk>\n" +
            "\t<git_branch>${gitBranch}</git_branch>\n" +
            "\t<git_commit_id>${gitCommitId}</git_commit_id>\n" +
            "\t<system_app>${isSystemApp}</system_app>\n" +
            "\t<dependencies>\n\t</dependencies>\n" +
            "</project>"

    def root = new XmlSlurper().parseText(info)

    // 获取依赖模块的信息
    //noinspection GroovyAssignabilityCheck
    configurations.findAll {
        it.name.equals('compile') || it.name.equals('releaseCompile')
    }.each {
        it.dependencies.all { d ->
            if (null != d.group && null != d.version) {
                root.dependencies.appendNode {
                    //noinspection GroovyAssignabilityCheck
                    dependency(name: d.name) {
                        package_name d.group
                        version_name d.version
                    }
                }
                file.write(XmlUtil.serialize(root))
            }
        }
    }
}

// 针对 release 版本的条件校验任务
task checkReleaseConditions << {

    if (!endsWith00()) { // 如果版本名不符合规定，则不能生成 release 版本
        throw new RuntimeException('Release version name should ends with "00".')
    }

    if (needCommit()) { // 如果还有改动没有通过 git 提交，则不能生成 release 版本
        throw new RuntimeException('Please commit the changes before you build release version.')
    }

}

/**************** 任务定义 - 结束 ****************/

// 在编译前，首先执行“创建信息文件”的任务
preBuild.dependsOn createAppInfo

// 如果是 release 版本，则需要检查编译条件
assembleRelease.dependsOn checkReleaseConditions

/************************ 搜集项目信息 - 结束 ************************/

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'com.linkin.tvlayout:tvlayout:1.0.13'
    compile 'com.linkin.base:base:1.0.91'
    compile 'com.jakewharton:butterknife:7.0.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
